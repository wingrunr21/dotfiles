function prompt_ir_black_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_ir_black_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook
  add-zsh-hook precmd prompt_ir_black_precmd

  # Text
  zstyle ':omz:module:git' action '%s'
  zstyle ':omz:module:git' commit '(%.7c)'
  zstyle ':omz:module:git' branch '%b'

  # Indicators
  zstyle ':omz:module:git' added ' %%B%F{green}✚%f%%b'
  zstyle ':omz:module:git' ahead ' %%B%F{yellow}⬆%f%%b'
  zstyle ':omz:module:git' behind ' %%B%F{yellow}⬇%f%%b'
  zstyle ':omz:module:git' deleted ' %%B%F{red}✖%f%%b'
  zstyle ':omz:module:git' modified ' %%B%F{blue}✱%f%%b'
  zstyle ':omz:module:git' renamed ' %%B%F{magenta}➜%f%%b'
  zstyle ':omz:module:git' stashed ' %%B%F{cyan}✭%f%%b'
  zstyle ':omz:module:git' unmerged ' %%B%F{yellow}═%f%%b'
  zstyle ':omz:module:git' untracked ' %%B%F{white}◼%f%%b'
  zstyle ':omz:module:git' info \
       'prompt'  ' %F{red}($(coalesce "%b" "%c|%s"))' \
       'rprompt' '%A%B%S%a%d%m%r%U%u'

  PROMPT='%F{black}%n@%m %F{green}%~${git_info:+${(e)git_info[prompt]}}%(?.%F{green} > .%F{red} > )%F{reset}'
  RPROMPT='${git_info[rprompt]}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_ir_black_setup "$@"
